# Potionq区块移动

既然我们知道了区块移动是如何工作的，那我们现在就可以把这些命令映射成可以在Portion文件里工作的方式了。

首先我们需要确定Potion文件里的“区块”是什么。因为我们有两组可以使用的区块移动命令，所以我们可以使用两组“模式”，这样我们的用户就可以选择使用他们自己喜欢的。

我们使用下面两种模式来定义Portion区块的开始：

- 1. 所有后面跟着一个空行并且是以非空字符开始的行，或者是文件的第一行。
- 2. 所有第一个字符非空，并且行内包含等号，且该行以冒号结尾。

现在使用简单扩展后的`factorial.pn`的文件，下面是我们的规则中会作为区块头的样子：

<pre><code
# factorial.pn                              1
# Print some factorials, just for fun.

factorial = (n):                            1 2
    total = 1

    n to 1 (i):
        total *= i.

    total.

print_line = ():                            1 2
    "-=-=-=-=-=-=-=-\n" print.

print_factorial = (i):                      1 2
    i string print
    '! is: ' print
    factorial (i) string print
    "\n" print.

"Here are some factorials:\n\n" print       1

print_line ()                               1
10 times (i):
    print_factorial (i).
print_line ()
</code></pre>

我们的第一条规则定义的比较自由。它直接把一个“顶级的文本块”定义成一个区块。

第二个定义就比较严格一点了。它把一个区块定义为一个函数定义。

# 自定义映射

在插件的代码库里创建一个`ftplugin/option/sections.vim`文件。这里我们将会放入区块移动相关的代码。记住，只要缓冲区的`filetype`是`potion`，这里的代码就会执行。

我们要重新映射全部的4个命令，所以我们先创建一个大致的“框架性”文件：
<pre><code>
noremap <script> <buffer> <silent> [[ <nop>
noremap <script> <buffer> <silent> ]] <nop>

noremap <script> <buffer> <silent> [] <nop>
noremap <script> <buffer> <silent> ][ <nop>
</code></pre>

注意我们用了`noremap`而不是`nnoremap`命令，因为我们想让这些命令能够在操作符待定模式下也能够使用。这样的话，你就可以使用`d]]`这样的命令来完成“删除从当前位置到下一个区块”的动作。

我们让这些命令是缓冲区本地的，这样他们就只会作用在Potion文件上，而不是全局有效。

我们把这些命令设置成静默模式，因为用户不会关心区块直接移动的具体细节的。

# 使用函数

对于这几个命令而言，区块移动的代码逻辑都是很相似的，所以我们可以把这些代码封装到一个函数里，来供我们的映射调用。

你会在很多Vim的插件里看到类似这样的策略。这样的代码比一大堆的映射代码更加易于阅读和维护。

修改`sections.vm`文件，包含如下内容：
<pre><code>
function! s:NextSection(type, backwards)
endfunction

noremap <script> <buffer> <silent> ]]
        \ :call <SID>NextSection(1, 0)<cr>

noremap <script> <buffer> <silent> [[
        \ :call <SID>NextSection(1, 1)<cr>

noremap <script> <buffer> <silent> ][
        \ :call <SID>NextSection(2, 0)<cr>

noremap <script> <buffer> <silent> []
        \ :call <SID>NextSection(2, 1)<cr>
</code></pre>

这里我使用了Vim的行连续特性，因为这里的代码行的长度对我而言有点超长了。注意反斜杠符号是怎么在第二行的开头来续上第一行的。阅读`help line-continuation`来了解更多内容。

注意我们使用了<SID>以及一个本地的函数来避免全局的命名空间被我们的帮助函数所污染。

每个映射只是简单地用合适的参数来调用`NextSection`函数来达到移动的目的。现在我们可以开始来实现`NextSection`函数了。

# 基本移动

我们先想想我们的函数需要做些什么。我们想把光标移动到下一个“区块”，一个很方便的能够移动光标的方式就是通过`/`和`?`命令。

编辑`NextSection`函数，内容如下：
<pre><code>
function! s:NextSection(type, backwards)
    if a:backwards
        let dir = '?'
    else
        let dir = '/'
    endif

    execute 'silent normal! ' . dir . 'foo' . "\r"
endfunction
</code></pre>

这个函数里使用了我们之前已经知道的`execute normal!`模式来完成`/foo`和`?foo`的功能，具体使用哪个取决于`backwards`的值。这样开始很不错。

继续下去，我们很显然不是要查找像`foo`这样的内容，而且我们要查找的文本模式依赖于我们究竟是用区块的第一个还是第二个区块头的定义。

编辑`NextSection`的内容如下所示：
<pre><code>
function! s:NextSection(type, backwards)
    if a:type == 1
        let pattern = 'one'
    elseif a:type == 2
        let pattern = 'two'
    endif

    if a:backwards
        let dir = '?'
    else
        let dir = '/'
    endif

    execute 'silent normal! ' . dir . pattern . "\r"
endfunction
</code></pre>

我们现在需要做的只是把文本的模式填进去，那我们就直接开始把。

# 顶级文本块

用下面的文本替换第一个`let pattern = '..' `的文本行：
<pre><code>
let pattern = `\v(\n\n^\S|%^)`
<code></pre>

要理解上面的正则表达式，只需要回忆一下我们之前所要实现的“区块”的定义：

	所有后面跟着一个空行并且是以非空字符开始的行，或者是文件的第一行。

第一行开头的`\v`命令直接强制进入我们之前见过的`非常魔法`模式。

这个正则表达式剩下的就是有两个选项组成的分组。首先,`\n\n^\S`会查找“一个新行，它后面跟着另外一个新行，还跟着一个非空的字符”。这是我们定义里的第一种。

另外一个正则项是`%^`，这是Vim正则里的一个特殊的元字符，表示“文件的开始”。

现在，我们已经到了可以尝试第一组映射的时候了。保存`ftplugin/potion/sections.vm`文件，然后在你的Potion文件里运行`:set filetype=potion`。这个时候，`[[`和`]]`命令应该能够起作用了，不过它们还是有点奇怪的。

# 搜索标志

你会发现当你在区块间移动时，你的光标实际上在真正需要跳到的行的上一行。在继续阅读之前先思考一下为什么会是这样的结果。

答案是当我们使用`/`或者`?`进行搜索的时候，Vim或默认把光标移动到匹配项的开始处。例如，让我们使用`/foo`命令的时候，你的光标会出现在`foo`的`f`上。

要让Vim把光标放在匹配到的字符串的末尾而不是开始处，我们可以使用搜索标识符。在你的Potion文件里，尝试下面的搜索命令：
<pre><code>
/factorial/e
</code></pre>

Vim会查找`factorial`这个单词，并且把你带到这个单词所在的位置。按`n`来在几个匹配的文本之间移动。标识符`e`告诉Vim来把光标移动到匹配字符串的末尾处。再试试反向查找的命令：
<pre><code>
?factorial?e
</code></pre>

我们来修改我们的函数，使用搜索标识符来把光标放到匹配的区块的下一行的末尾：
<pre><code>
function! s:NextSection(type, backwards)
    if a:type == 1
        let pattern = '\v(\n\n^\S|%^)'
        let flags = 'e'
    elseif a:type == 2
        let pattern = 'two'
        let flags = ''
    endif

    if a:backwards
        let dir = '?'
    else
        let dir = '/'
    endif

    execute 'silent normal! ' . dir . pattern . dir . flags . "\r"
endfunction
</code></pre>

这里我们做了两处改动。首先，我们根据区块移动的类型设置`type`变量的值。现在我们只需要关心第一种类型，这种情况下需要一个`e`标识。

其次，我们需要把`dir`和`flags`拼接到搜索串后面。这样会根据搜索的方向添加上`?e`或者`/e`。

保存文件，切换会你的Potion文件，并且运行`:set ft=potion`来让改动生效。现在再试试`[[`和`]]`，它们能够很好地运行了！

# 函数定义

现在来实现我们的第二个“区块”的定义了，幸运的是，这个比上一个简单明了。会议一下我们之前的定义：

	所有第一个字符非空，并且行内包含等号，且该行以冒号结尾。

我们可以用一个很简单的正则来查找这些行。修改函数中的第二个`let pattern = '...'`为：
<pre><code>
let pattern = '\v^\S.*\=.*:$'
</code></pre>

这个函数看起来比第一个简单，就作为一个练习留给读者自己去理解了 —— 它是对上面的定义的一个很直观的表示。

保存文件，在`factorial.pn`中运行`:set filetype=potion`，然后试试新的`][`和`[]`映射。它们应该能够正常运行。

这里我们不需要搜索标识，因为把光标放在匹配行的开始（默认模式）就可以了。

# 可视模式

我们的区块移动明了在normal模式下运行的很好，现在我们需要添加一点东西，让它们也能够在visual模式下工作。首先，把函数修改成如下所示：
<pre><code>
function! s:NextSection(type, backwards, visual)
    if a:visual
        normal! gv
    endif

    if a:type == 1
        let pattern = '\v(\n\n^\S|%^)' 
        let flags = 'e'
    elseif a:type == 2
        let pattern = '\v^\S.*\=.*:$'
        let flags = ''
    endif

    if a:backwards
        let dir = '?'
    else
        let dir = '/'
    endif

    execute 'silent normal! ' . dir . pattern . dir . flags . "\r"
endfunction
</code></pre>

这里有两点改动。首先，添加了一个额外的参数，用来标识函数是否是在visual模式下调用的。其次，如果是在visual模式下调用的，我们运行`gv`来保存可视模式下选择的内容。

为什么需要这样做？我们来解释一下为什么需要这么做。在visual模式下选择一些文本，然后运行下面的命令：
<pre><code>
:echom "hello"
</code></pre>

会输出`hello`但是在visual模式下选中的文本会被清除掉！

当在ex模式下通过`:`运行命令，会导致visual模式下选择的文本被清除。`gv`命令会重新选择之前选择的文本，相当于“撤销”了清除的动作。这是一个很有用的命令，而且可以在你的日常工作中使用。

现在，我们需要更新现有的映射，给新的`visual`参数传入`0`。
<pre><code>
noremap <script> <buffer> <silent> ]]
        \ :call <SID>NextSection(1, 0, 0)<cr>

noremap <script> <buffer> <silent> [[
        \ :call <SID>NextSection(1, 1, 0)<cr>

noremap <script> <buffer> <silent> ][
        \ :call <SID>NextSection(2, 0, 0)<cr>

noremap <script> <buffer> <silent> []
        \ :call <SID>NextSection(2, 1, 0)<cr>
</code></pre>

现在没有什么复杂的内容了。我们来把最后剩下的visual模式下的映射也加入到我们的代码中：
<pre><code>
vnoremap <script> <buffer> <silent> ]]
        \ :<c-u>call <SID>NextSection(1, 0, 1)<cr>

vnoremap <script> <buffer> <silent> [[
        \ :<c-u>call <SID>NextSection(1, 1, 1)<cr>

vnoremap <script> <buffer> <silent> ][
        \ :<c-u>call <SID>NextSection(2, 0, 1)<cr>

vnoremap <script> <buffer> <silent> []
        \ :<c-u>call <SID>NextSection(2, 1, 1)<cr>
</code></pre>


这些映射全部都给参数`visual`传入`1`来告诉Vim在执行动作之前重新选择之前的选择，这里也用到了我们之前在Grep的章节里学习到的关于`<c-u>`的技巧。

# Why Bother?

对于这个看起来比较小的功能点而言，这个章节显得有点长，但是这个过程中，你已经学习并且练习了很多有用的东西：

- 使用`noremap`代替`nnoremap`来创建进行操作或者动作的命令。
- 使用Vimscript来一步一步地构建一个功能。
- 通过编码来创建一个`execute 'normal ! ...`命令.
- 通过正则和简单地搜索来进行移动。
- 使用特殊的类似`%^`(文件的开始)的元操作符。
- 使用查找标识符来控制查找的行为。
- 操作visual模式下的映射，保持那些需要保持之前选择的文本。

继续下去完成后面的练习（只是简单的阅读`:help`的内容），然后弄点冰淇淋。它是你完成这个章节应得得奖励！

# 练习

- 阅读`:help search()`。这是一个很实用的命令，你也可以尝试使用`/`和`?`的搜索标识符。
- 阅读`:help ordinary-atom`的内容，学习一些你可以在正则表达式中实用的更有趣的内容。






